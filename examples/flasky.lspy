#! /usr/bin/env sibilant


(def import-from flask Flask escape)
(def import-from werkzeug.routing parse_rule)


(defmacro url-rule (name rule . body)
  (define formals
    (iter-each ((converter wut name) (parse_rule (str rule)))
	       (symbol name) unless: (None? converter)))

  `(app.add_url_rule ,(str rule)
		     endpoint: ,(str name)
		     view_func: (function ,name (,@formals) ,@body)))


(defmacro template strings
  `(str.format (str.join " " (map str (#tuple ,@strings))) **: (locals)))


(defmacro declare-app (name)
  `(define app (Flask ,(str name))))


;;;


(declare-app flasky)


(url-rule
 index /

 (define lcls (escape (locals)))
 (template
  "<p>This is a simple web app. What more do you want?</p>"
  "<code>{lcls}</code>"))


(url-rule
 reverse-words /reverse/<words>

 (define rwords (escape (item-slice words step: -1)))
 (define words (escape words))
 (define lcls (escape (locals)))

 (#str
  "<code>\n"
  "<p>\U0001f615 Original: " words "</p>\n"
  "<p>\U0001f60e Reversed: " rwords "</p>\n"
  "<p>Locals: " lcls "</p></code>\n"))


;; you can either run this directly, or you can compile it to a .pyc
;; and invoke it from the flask cli
(when-main
 (app.run))


;; the end.
