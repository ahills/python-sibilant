;; This library is free software; you can redistribute it and/or modify
;; it under the terms of the GNU Lesser General Public License as
;; published by the Free Software Foundation; either version 3 of the
;; License, or (at your option) any later version.
;;
;; This library is distributed in the hope that it will be useful, but
;; WITHOUT ANY WARRANTY; without even the implied warranty of
;; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
;; Lesser General Public License for more details.
;;
;; You should have received a copy of the GNU Lesser General Public
;; License along with this library; if not, see
;; <http://www.gnu.org/licenses/>.


(def import sys)
(def import-from argparse ArgumentParser)
(def import-from os.path basename)

(def import-from .curse cli)


(defun create-option-parser [name]
  " the siblife CLI argument parser "

  (define parser (ArgumentParser
		  prog: (basename name)
		  description: "This is siblife"))

  (parser.add_argument "--wrap" action: "store_true" default: False
		       help: "Game board wraps at edges")

  (define g (parser.add_mutually_exclusive_group))

  (g.add_argument "--smilies" action: "store_const" dest: "renderer"
		  const: "smilies"
		  help: "use the smilies renderer")

  (g.add_argument "--digits" action: "store_const" dest: "renderer"
		  const: "digits"
		  help: "use the digits renderer")

  (g.add_argument "--default" action: "store_const" dest: "renderer"
		  const: "default" default: "default"
		  help: "use the default renderer")

  parser)


(defun main [argv: None]
  " entry point for the siblife CLI "

  ;; grab sys.argv if we aren't given something else
  (when (None? argv) (setq argv sys.argv))
  (define-values [name . args] argv)

  (var [parser (create-option-parser name)]
       [options (parser.parse_args args)])

  (try
   (cli options)

   [[KeyboardInterrupt as: ki]
    (print file: sys.stderr)
    130]

   [else: 0]))


(when-main
  (sys.exit (main)))


;; The end.
